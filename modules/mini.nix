{ config, pkgs, lib, ... }:

let
  # Read hostname from local config file (generated by bootstrap)
  hostnameFile =
    if builtins.pathExists /etc/nix-darwin/hostname.local
    then /etc/nix-darwin/hostname.local
    else ../hostname.local;
  hostname = if builtins.pathExists hostnameFile
    then lib.strings.trim (builtins.readFile hostnameFile)
    else "mini-default";

  # Read username for activation script
  usernameFile =
    if builtins.pathExists /etc/nix-darwin/username.local
    then /etc/nix-darwin/username.local
    else ../username.local;
  username = if builtins.pathExists usernameFile
    then lib.strings.trim (builtins.readFile usernameFile)
    else "admin";
in
{
  # Set hostname to mini-${serial_number}
  networking = {
    hostName = hostname;
    computerName = hostname;
    localHostName = hostname;
  };

  # Install orchard for VM orchestration and colima for containers
  environment.systemPackages = [
    pkgs.orchard
    pkgs.colima
    pkgs.tailscale
  ];

  # Start colima on boot
  launchd.user.agents.colima = {
    command = "${pkgs.colima}/bin/colima start --foreground";
    serviceConfig = {
      Label = "com.colima.default";
      RunAtLoad = true;
      KeepAlive = true;
      StandardOutPath = "/tmp/colima.stdout.log";
      StandardErrorPath = "/tmp/colima.stderr.log";
    };
  };

  # Load colima agent during activation (for first-time bootstrap)
  system.activationScripts.postActivation.text = ''
    echo "Loading colima launchd agent for user ${username}..."
    COLIMA_PLIST="/Users/${username}/Library/LaunchAgents/com.colima.default.plist"
    if [ -f "$COLIMA_PLIST" ]; then
      sudo -u ${username} launchctl unload "$COLIMA_PLIST" 2>/dev/null || true
      sudo -u ${username} launchctl load "$COLIMA_PLIST" || true
    fi
  '';
}
